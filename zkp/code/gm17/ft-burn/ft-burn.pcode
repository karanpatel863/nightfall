import "LIBSNARK/sha256compression"
import "PACKING/unpack128"

def CONCAT128x216x216('field' a127...0, 'field' b215...0, 'field' c215...0)->(field560):
	return a127...0, b215...0, c215...0

def CONCAT216x216('field' a215...0, 'field' b215...0)->(field432):
	return a215...0, b215...0

//this function is needed because we've used * as a pcode wildcard and ORDEREDCONCAT needs a multiply doh!
def MUL(field x, field y)->(field1):
	return x*y

//concatenate two words according to an order bit. If order=0, g is first.  If order=1, f is first
def ORDEREDCONCAT216x216(field order, 'field' f215...0, 'field' g215...0) ->(field432):
	{field r* = MUL(order,f*) + MUL(1-order, g*)
	field s* = MUL(order,g*) + MUL(1-order, f*)}215...0
	return r215...0,s215...0

def shaPad432To512('field' a431...0)->(field512):
	return a431...0, 1, 70....0, 1,1,0,1,1,0,0,0,0

def shaPad216To512('field' a215...0)->(field512):
	return a215...0, 1, 287....0, 1,1,0,1,1,0,0,0

def shaPad128To512('field' a127...0)->(field512):
	return a127...0, 1, 375....0, 1,0,0,0,0,0,0,0


//inputs for main are:
//Alice's original coins are C and D

// payTo, the public Ethereum address to which the burned tokens get paid
// C, the amount contained in coin z_C (public)
// Alice's secret key SkA (private)
// Sc the serial number for coin z_C (private)
// c0b-c31b - the Merkle path for C (private)

//Nc the nullifier for C (public)

// R - the Merkle root (public)

// Note - We truncate the 256 bit SHA hash down to 216 bits as it's a better fit with our coding scheme

def main('field' payTo1...0, field C, 'field' £pSkA1...0, 'field' £pSc1...0, [private field c*bh, private field c*bl]0...31, private field order, 'field' pNc1...0, 'field' pR1...0)->(field1):

//Unpack the inputs of main() to 128 bits. We'll unpack each field to its own 128 bit string for simplicity for now. Later efficiencies could be made by grouping some inputs.

// We don't need to unpack payTo, because it isn't part of a hash.

C127...0 = unpack128(C)

SkA255...128 = unpack128(pSkA1)
SkA127...0 = unpack128(pSkA0)

Sc255...128 = unpack128(pSc1)
Sc127...0 = unpack128(pSc0)

{c*b255...128 = unpack128(c*bh)}31...0
{c*b127...0 = unpack128(c*bl)}31...0

order127...0 = unpack128(order)

Nc255...128 = unpack128(pNc1)
Nc127...0 = unpack128(pNc0)

R255...128 = unpack128(pR1)
R127...0 = unpack128(pR0)

//*************first we construct a proof that the nullifier Nc is correct************
	//concat Sc|Sk. The padding needs to be added to create a true hash

		t431...0 = CONCAT216x216(Sc215...0, SkA215...0)
		t511...0 = shaPad432To512(t431...0)
		nc255...0 = sha256compression(t511...0)

// *********next, calculate Alice's public key from her private key: PkA = H(SkA) truncated to 64 bits.  This will be useful later

		t511...0 = shaPad216To512(SkA215...0)
		PkA255...0 = sha256compression(t511...0)

//*************Prove that Zc is in the Merkle tree

// ***First calculate Zc = H(C|PkA|S)

	d559...0 = CONCAT128x216x216(C127...0, PkA215...0, Sc215...0)

	//this is too big to handle in a single sha round and our sha can't handle multiple rounds
	//as a work-around, we will split d, hash both parts and then hash the result - remember to do this in Node too!

	//first 432 bits (432 is 216*2 - a convenient number in our 216 bit scheme)
	t511...0 = shaPad432To512(d431...0)
	t255...0 = sha256compression(t511...0)
		//remaining 128 bits
	u511...0 = shaPad128To512(d559...432)
	u255...0 = sha256compression(u511...0)
		//then concat the results msb first
	d431...0 = CONCAT216x216(u215...0, t215...0)
		//and hash the result
	d511...0 = shaPad432To512(d431...0)
	t255...0 = sha256compression(d511...0)  //zc

//concat 1st through 31st siblings

	{t431...0 = ORDEREDCONCAT216x216(order*, t215...0, c*b215...0)

	t511...0 = shaPad432To512(t431...0)

	t255...0 = sha256compression(t511...0)}31...1

// concat with the 32nd sibling. This should equal the root.

	t431...0 = ORDEREDCONCAT216x216(order0, t215...0, c0b215...0)

	t511...0 = shaPad432To512(t431...0)

	rc255...0 = sha256compression(t511...0)


//**************Now test the proofs

	Nc==215...0==nc
	R==215...0==rc

//**************Intertwine payTo in the proof (trivially)

	payTo1 * 1 == payTo1
	payTo0 * 1 == payTo0

	return 1
